// You are given two qubits in state |00âŸ©. Your task is to prepare the following state on them:
// 1/âˆš3(|01âŸ©+|10âŸ©+|11âŸ©)

import Std.Diagnostics.DumpRegister;

operation Main() : Unit
{    
    // Two qubits in state |00âŸ©
    use qs = Qubit[2];

    // Your task is to prepare the following state on them:
    // 1/âˆš3(|01âŸ©+|10âŸ©+|11âŸ©)
    Solve(qs);

    // Check the result
    DumpRegister(qs);
    // Basis | Amplitude      | Probability | Phase
    // --------------------------------------------
    // |01âŸ©  | 0.5774+0.0000ğ‘–  | 33.3333%    | 0.0000
    // |10âŸ©  | 0.5774+0.0000ğ‘–  | 33.3333%    | 0.0000
    // |11âŸ©  | 0.5774+0.0000ğ‘–  | 33.3333%    | 0.0000

    // Reset the qubits
    MResetEachZ(qs);
}

operation Solve (qs : Qubit[]) : Unit {
    // An additional qubit to be used as a ancilla qubit or accessory qubit.
    use q = Qubit();

    // Keep a result mutable variable.
    mutable res = One;

    repeat 
    {
        // Put the input qubits into a superposition of 1/2(|00âŸ© + |01âŸ© + |10âŸ© + |11âŸ©)
        ApplyToEach(H, qs);

        // Apply the CCNOT gate with the input qubits and the ancilla qubit as a target.
        // The ancilla qubit will be in state |1âŸ© if the input qubits are in state |11âŸ©.
        // The input qubits will be in state |00âŸ©, |01âŸ© or |10âŸ© if the ancilla qubit is in state |0âŸ©.
        Controlled X(qs, q);

        // Measure the ancilla qubit and store the result.
        set res = MResetZ(q);
    } 
    // Repeat until the ancilla qubit is in state |0âŸ© after measurement.
    until (res == Zero)
    // Reset the input qubits to their initial state |00âŸ© for the next iteration (if needed).
    fixup {
        ResetAll(qs);
    }

    // Apply the X gate to the input qubits to convert the state
    // 1/âˆš3(|00âŸ© + |01âŸ© + |10âŸ©) to 1/âˆš3(|11âŸ© + |10âŸ© + |01âŸ©).
    ApplyToEach(X, qs);
}